// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  repositories {
    google()
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.3.0'
    classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7'
    classpath 'org.aspectj:aspectjtools:1.8.10'
    classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'

    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

subprojects { project ->
  group = GROUP
  version = VERSION_NAME

  apply plugin: 'checkstyle'
  apply plugin: 'maven'

  task checkstyle(type: Checkstyle) {
    configFile rootProject.file('checkstyle.xml')
    source 'src/main/java'
    ignoreFailures false
    showViolations true
    include '**/*.java'

    classpath = files()
  }

  afterEvaluate {
    if (project.tasks.findByName('check')) {
      check.dependsOn('checkstyle')
    }
  }

  buildscript {
    repositories {
      google()
      mavenCentral()
    }
    dependencies {
      classpath 'com.android.tools.build:gradle:3.3.0'
    }
  }

  repositories {
    google()
    jcenter()
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

ext {
  minSdkVersion = 8
  targetSdkVersion = 28
  compileSdkVersion = 28
  buildToolsVersion = "28.0.3"
  sourceCompatibilityVersion = JavaVersion.VERSION_1_7
  targetCompatibilityVersion = JavaVersion.VERSION_1_7
}

ext.deps = [

        // AspectJ
        aspectjRuntime: "org.aspectj:aspectjrt:1.8.10",
        aspectjTools  : "org.aspectj:aspectjtools:1.8.10",

        // Test dependencies
        junit         : 'junit:junit:4.12',
        truth         : 'com.google.truth:truth:0.28',
        robolectric   : 'org.robolectric:robolectric:3.1',
        mockito       : "org.mockito:mockito-core:1.10.19"
]